class Ball {
  PVector location;
  PVector velocity;
  PVector acceleration;
  PVector gravity;
  float bounceLoss;
  float radius;
  color col;

  Ball(PVector l_, PVector v_, PVector g_, float r_) {
    location = l_;
    velocity = v_;
    gravity = g_;
    acceleration = gravity;
    radius = r_;
    bounceLoss = random(0.08,0.2);
    col = color(random(0,100), random(60,100), random(60,100));
  }

  void drawMe() {
    fill(col);
    circle(location.x, location.y, radius*2);
  }

  void move() {
    velocity.add(acceleration);
    location.add(velocity);
  }

  void bounce(PVector topLeft, PVector bottomRight) {
    float left = topLeft.x + radius;
    float right = bottomRight.x - radius;
    float top = topLeft.y + radius;
    float bottom = bottomRight.y - radius;
    boolean bouncedX = clampX(left, right);
    boolean bouncedY = clampY(top, bottom);
    if (bouncedX) {
      velocity.x *= -(1-bounceLoss);
      velocity.y *= 1 - (bounceLoss / 5);
    }
    if (bouncedY) {
      velocity.y *= -(1-bounceLoss);
      velocity.x *= 1 - (bounceLoss / 5);
    }
  }
  
  void pullTo(PVector pullPos, float pullStrength){
    PVector dir = PVector.sub(pullPos, location);
    dir.normalize();
    dir.mult(pullStrength);
    acceleration = dir;
  }
  
  void noPull(){
    acceleration = gravity;
  }

  boolean clampX(float min, float max) {
    float last = location.x;
    location.x = max(location.x, min);
    location.x = min(location.x, max);
    return last != location.x;
  }

  boolean clampY(float min, float max) {
    float last = location.y;
    location.y = max(location.y, min);
    location.y = min(location.y, max);
    return last != location.y;
  }
}
